$ ->
  class Console
    constructor: (target, @process) ->
      @buffer = ""
      @cursorInterval = undefined

      @$target = $(target)
      @$target.keydown(@keydown)
      @$target.keypress(@keypress)

      @$console = $('.console', @$target)
      @$cursor = $('span.cursor', @$console)
      @$entry = $('span.entry', @$console)

      @resetCursor()

    type: (c) ->
      @buffer += c
      @update()

    append: (text) ->
      @$entry.before(text)
      @$console.stop().animate({ scrollTop: @$console[0].scrollHeight }, 100)

    resetCursor: ->
      clearInterval(@cursorInterval) if @cursorInterval?
      @$cursor.show()
      @cursorInterval = setInterval( =>
        @$cursor.toggle()
      , 1000)

    update: ->
      @resetCursor()
      @$entry.empty()

      appendWithClass = (c, klass) =>
        @$entry.append($("<span/>").text(c).addClass(klass))

      paren = -1
      string = false
      quote = false
      for c in @buffer
        if c == " "
          @$entry.append("&nbsp;")
        else if string
          if quote
            appendWithClass(c, "string")
            quote = false
          else if c == "\\"
            appendWithClass(c, "string")
            quote = true
          else if c == "\""
            appendWithClass(c, "string")
            string = false
          else
            appendWithClass(c, "string")
        else if c == "\""
          appendWithClass(c, "string")
          string = true
        else if c in ["(", "["]
          paren += 1
          appendWithClass(c, "paren#{paren % 4}")
        else if c in [")", "]"]
          appendWithClass(c, "paren#{paren % 4}")
          paren -= 1
        else
          @$entry.append(c)

    keydown: (e) =>
      if e.keyCode == 8
        e.preventDefault()
        @buffer = @buffer.substr(0, @buffer.length - 1)
        @update()
      else if e.keyCode == 13
        e.preventDefault()
        content = @$entry.html()
        @append(content)
        @append($("<br>"))
        @$entry.html('')

        @process(@buffer)
        @buffer = ''
      else if e.keyCode == 9
        e.preventDefault()
        @type("\t")

    keypress: (e) =>
      if e.which == 21
        e.preventDefault()
        @buffer = ""
        @update()
      else if e.which >= 32
        e.preventDefault()
        @type(String.fromCharCode(e.which))

  process = (command) ->
    $.ajax
      url: "/shell/execute"
      type: "POST"
      data:
        command: command
      success: (result) ->
        if result.ok
          if result.stdout
            console.append($("<span/>").addClass("stdout").text(result.stdout))
          console.append($("<span/>").addClass("result").text(result.result))
          console.append($("<br>"))
          console.append($("<span/>").addClass("prompt").text("user=> "))
        else
          console.append($("<span/>").addClass("error").text(result.error))
          console.append($("<br>"))
          console.append($("<span/>").addClass("prompt").text("user=> "))

  console = new Console($('body'), process)
