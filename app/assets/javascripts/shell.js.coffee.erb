$ ->
  class Console
    constructor: (@process) ->
      @buffer = ""
      @cursorInterval = undefined
      @resetCursor()

    addChar: (c) ->
      @buffer += c
      @update()

    resetCursor: ->
      clearInterval(@cursorInterval) if @cursorInterval?
      $('span.cursor').show()
      @cursorInterval = setInterval( ->
        $('span.cursor').toggle()
      , 1000)

    update: ->
      @resetCursor()
      $('span.entry').empty()

      paren = -1
      string = false
      quote = false
      for c in @buffer
        if c == " "
          $('span.entry').append("&nbsp;")
        else if string
          if quote
            $('span.entry').append("<span class='string'>#{c}</span>")
            quote = false
          else if c == "\\"
            $('span.entry').append("<span class='string'>\\</span>")
            quote = true
          else if c == "\""
            $('span.entry').append("<span class='string'>\"</span>")
            string = false
          else
            $('span.entry').append("<span class='string'>#{c}</span>")
        else if c == "\""
          $('span.entry').append("<span class='string'>\"</span>")
          string = true
        else if c in ["(", "["]
          paren += 1
          $('span.entry').append("<span class='paren#{paren % 4}'>#{c}</span>")
        else if c in [")", "]"]
          $('span.entry').append("<span class='paren#{paren % 4}'>#{c}</span>")
          paren -= 1
        else
          $('span.entry').append(c)

    keydown: (e) =>
      if e.keyCode == 8
        e.preventDefault()
        @buffer = @buffer.substr(0, @buffer.length - 1)
        @update()
      else if e.keyCode == 13
        e.preventDefault()
        content = $('span.entry').html()
        $('span.entry').before(content)
        $('span.entry').before("<br>")
        $('span.entry').html('')
        command = @buffer
        @buffer = ''
        @process(command)
      else if e.keyCode == 9
        e.preventDefault()
        @addChar("\t")

    keypress: (e) =>
      if e.which == 21
        e.preventDefault()
        @buffer = ""
        @update()
      else if e.which >= 32
        e.preventDefault()
        @addChar(String.fromCharCode(e.which))

  process = (command) ->
    $.ajax
      url: "/shell/execute"
      type: "POST"
      data:
        command: command
      success: (result) ->
        if result.ok
          $('span.entry').before($("<span></span>").text(result.message))
          $('span.entry').before("<br>")
          $('span.entry').before("user=&gt; ")
        else
          $('span.entry').before("!?<br>")
          $('span.entry').before($("<span></span>").text(result.error))
          $('span.entry').before("<br>")
          $('span.entry').before("user=&gt; ")

  console = new Console(process)

  $('body').keydown console.keydown
  $('body').keypress console.keypress
